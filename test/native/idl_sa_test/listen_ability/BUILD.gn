# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("//build/test.gni")

idl_gen_interface("idl_sa_test_interface") {
  sources = [ "IListenAbility.idl" ]

  sources_common = [ "IFooCustom.idl" ]

  client_enable = true
  log_domainid = "0xD001718"
  log_tag = "idlTest"
}

config("listen_ability_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "include",
    "${target_gen_dir}",
  ]
}

ohos_source_set("idl_sa_proxy") {
  output_values = get_target_outputs(":idl_sa_test_interface")
  sources = filter_include(output_values,
                           [
                             "*_proxy.cpp",
                             "*_client.cpp",
                             "*_custom.cpp",
                           ])

  public_configs = [ ":listen_ability_config" ]

  defines = [ "DEVICE" ]

  deps = [ ":idl_sa_test_interface" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]

  part_name = "idl_tool"
  subsystem_name = "ability"
}

ohos_source_set("idl_sa_stub") {
  output_values = get_target_outputs(":idl_sa_test_interface")
  sources = filter_include(output_values, [ "*_stub.cpp" ])

  public_configs = [ ":listen_ability_config" ]

  defines = [ "DEVICE" ]

  deps = [ ":idl_sa_test_interface" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  part_name = "idl_tool"
  subsystem_name = "ability"
}

ohos_shared_library("idl_sa_test") {
  sources = [ "src/listen_ability.cpp" ]

  public_configs = [ ":listen_ability_config" ]

  defines = [ "DEVICE" ]

  deps = [
    ":idl_sa_proxy",
    ":idl_sa_stub",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  part_name = "idl_tool"
  subsystem_name = "ability"
}
